!--------------------------------------------------
!- December-27-20 3:28:03 PM
!- Import of : 
!- c:\users\mike\desktop\p\personal\cribbage.prg
!- Commodore 64
!--------------------------------------------------
10 POKE53280,11:POKE53281,12:POKE54287,255:POKE54290,129:REM FREQ AND RND
20 POKE52,159:POKE54,159:POKE56,159:CLR:REM RAM FOR MLSHUFFLER AND DECK
30 FORX=1TO70:READY:NEXT:FORX=40912TO40958:READY:POKEX,Y:NEXT:RESTORE
40 REM PUT ML SHUFFLER INTO RAM AND RESET DATA PTR
50 REM DATA FOR SCORING LOGIC
60 DATA0,1,2,3,0,1,2,4,0,2,3,4,1,2,3,4,0,1,3,4,0,1,2,0,1,3,0,1,4,0,2,3,0,2,4
70 DATA0,3,4,1,2,3,1,2,4,1,3,4,2,3,4,0,1,0,2,0,3,0,4,1,2,1,3,1,4,2,3,2,4,3,4
80 REM DATA FOR ML SHUFFLE ROUTINE
90 DATA120,169,54,168,153,3,159,136,16,250,162,0,173
100 DATA27,212,240,251,201,53,176,247,133,254,138,168,185
110 DATA3,159,197,254,240,236,136,16,246,165,254,157,3
120 DATA 159,232,224,52,208,223,88,96
130 GOSUB170:G=X:HS=X:GOSUB190:REM INITIALIZE
140 PRINT"{clear}"
150 GOTO230
160 GOSUB170:G=X:HS=X:GOTO510
170 X=.:Y=X:T=X:P=X:A=X:B=X:C=X:D=X:Q=X:SX=X:SY=X:CT=X:L=X:H=X:V=X:K=X:J=X:M=X
180 O=1192:RETURN
190 DIMR(5,5),S(4,4),F(5,5),Z(52),SC(50),M$(10),T(6)
200 B$="UCCCCI{down}{left*6}B{space*4}B{down}{left*6}B{space*4}B{down}{left*6}B{space*4}B{down}{left*6}B{space*4}B{down}{left*6}JCCCCK"
210 BL$="{home}{space*39}{home}":Y$="{down*3}"
220 S$="ASXZ":C$="a23456789tjqk":K$="{black}{red}{black}{red}":D$="{down*10}":RETURN
230 PRINT"{clear}{dark gray}{down*4}{space*5}welcome to cribbage squares"
240 PRINT"{down}{space*5}a game of skill and chance."
250 GOSUB260:GOTO510:REM CHECK DISK/PLAYER OPTIONS
260 B=0:OPEN15,8,15:CLOSE15:IFST=0THEN280:REM DRIVE IS ON
270 PRINT"{down}{right}{reverse on}drive is off.{reverse off} turn on, press return":WAIT197,13:POKE198,0
280 OPEN15,8,15,"i":GOSUB580:IFENTHEN570
290 OPEN1,8,2,"0:squares,s":GOSUB580:IFEN=0THEN330
300 PRINT"{clear}{right*4}{down}insert disk with "CHR$(34)"squares"CHR$(34)" on it"
310 PRINT"{right*5}{reverse on}press return when ready":WAIT197,13:POKE198,0
320 CLOSE1:CLOSE15:OPEN15,8,15:OPEN1,8,2,"0:squares,s":GOSUB580:IFENTHEN570
330 PRINT"{clear}{down}{right}please choose a name";:IFA$<>"3"THEN350
340 PRINT", blanks to create"
350 PRINT"{down*3}"
360 FORX=1TO10:INPUT#1,M$(X):GOSUB580:IFENTHEN570
370 PRINT"{right*4}{reverse on}"M$(X):NEXT:CLOSE1:CLOSE15:PRINT"{home}{down*4}{reverse off}";
380 IFY=0THENY=1
390 PRINT"{right*4}"M$(Y)" current player"
400 GETA$:IFA$=""THEN400
410 IFA$=CHR$(13)THEN490
420 IFA$<>"{up}"ANDA$<>"{down}"THEN400
430 Q=Y
440 IFA$="{down}"THENY=Y+1:IFY=11THENY=1
450 IFA$="{up}"THENY=Y-1:IFY=0THENY=10
460 PRINT"{home}{down*4}{right*4}{reverse on}"LEFT$(D$,Q)M$(Q)
470 PRINT"{home}{down*4}{right*4}{reverse off}"LEFT$(D$,Y)M$(Y)
480 GOTO400
490 IFLEFT$(M$(Y),1)="-"THENGOSUB1640:IFB=99THEN400
500 NP$=M$(Y):RETURN
510 PRINT"{clear}{down*3}{right*4}hello {black}"NP$"{dark gray} please select an option":PRINT"{down*2}{right*6}1] play"
520 PRINT"{right*6}2] view scores":PRINT"{right*6}3] change/create player"
530 PRINT"{right*6}4] delete a player":PRINT"{right*6}5] quit"
540 GETA$:X=VAL(A$):IFX<1ORX>5THEN540
550 ONXGOTO620,1080,230,1300:REM IF X=5 THEN FALLS THRU TO 520
560 REM PLAY,VIEW SCORE, CHANGE/CREATE PLAYER, DELETE PLAYER, QUIT
570 CLOSE1:CLOSE15:END
580 INPUT#15,EN$,EM$,ET$,ES$
590 EN=VAL(EN$):IFEN=0OREN=50OREN=51THENRETURN
600 PRINTBL$"{reverse on}"EN$" "EM$" "ET$" "ES$
610 PRINT" press return{space*26}":WAIT197,13:POKE198,0:RETURN
620 G=G+1:SYS40912:FORX=0TO5:FORY=0TO5:R(X,Y)=0:NEXT:NEXT
630 REM SHUFFLE DECK AND RESET ARRAY
640 CT=(INT(RND(1)*36)+17):REM CUT DECK
650 POKE53281,0:PRINT"{clear}":POKE53281,8:PRINT"{home}{down*4}{red} "B$:PRINT"{black} "B$
660 GOSUB1320:GOSUB1350:PRINT"{home}{down*4}{right*8}>":PRINTBL$
670 REM PRINT OUTLINE AND LOAD DECK
680 V=1:Q=Z(CT):REM CHOSEN CUT
690 IFQ-13>0THENQ=Q-13:V=V+1:GOTO690:REM CHECK SUIT
700 P=Q:H=Q:IFP>10THENP=10
710 FORT=0TO3:F(T,4)=V:F(4,T)=V:R(T,4)=Q:R(4,T)=Q:S(T,4)=P:S(4,T)=P:NEXT:P=0
720 IFH=11ORH=24ORH=37ORH=50THENP=P+16
730 FORJ=1TO16:Q=Z(J):A=1:PRINT"{home}{right*2}{down*5}";:L=0
740 IFQ-13>0THENQ=Q-13:A=A+1:GOTO740
750 GOSUB1630
760 GOSUB1360
770 IFZA$="b"THEN760
780 GOSUB1570
790 NEXT
800 PRINTBL$"game"G"score"P"-cont ?":SC(G)=P:IFSC(G)>HSTHENHS=SC(G)
810 GETZA$:IFZA$=""THEN810
820 IFZA$="y"THEN840
830 IFZA$="n"THEN880
840 A=0:PRINTBL$" ave.";
850 FORX=1TOG:A=A+SC(X):NEXT:PRINT(INT((A/G)*100+.5))/100"high"HS"{black}hit return{dark gray}"
860 WAIT197,13:POKE198,0:IFG=50THEN880
870 GOSUB170:GOTO620
880 POKE130,240:POKE131,239:POKE128,201:POKE129,32
890 PRINT"{clear}{down*3}{right*3}"NP$"'s scoring after"G"games{down}":POKE198,0
900 FORX=1TOG:PRINTSC(X),:NEXT:PRINT
910 PRINT"{down*2}{right*3}{black}return to cont":WAIT198,1:POKE198,0
920 PRINT"{clear}{red}{down*4}{right*4}updating; please wait.{dark gray}"
930 OPEN1,8,2,"0:"+NP$:OPEN15,8,15:POKE198,0:REM +",l,"+CHR$(5)
940 PRINT#15,"p"+CHR$(2)+CHR$(2)+CHR$(0)+CHR$(1):INPUT#1,Y$:X=HS
950 PRINT#15,"p"+CHR$(2)+CHR$(1)+CHR$(0)+CHR$(1):INPUT#1,HS$
960 HS=VAL(HS$):IFX>HSTHENHS=X
970 HS$=STR$(HS)
980 PRINT#15,"p"+CHR$(2)+CHR$(1)+CHR$(0)+CHR$(1):PRINT#1,HS$
990 PRINT#15,"p"+CHR$(2)CHR$(2)CHR$(0)CHR$(1):PRINT#1,STR$(Y+G)
1000 Y=VAL(Y$)-1:H=INT(Y/256):L=YAND255
1010 FORX=1TOG:L=L+1:IFL=256THENL=0:H=H+1
1020 PRINT#15,"p"+CHR$(2)CHR$(L)CHR$(H)CHR$(1)
1030 SC$=STR$(SC(X))
1040 PRINT#1,SC$:NEXT
1050 L=L+1:IFL=256THENL=0:H=H+1
1060 PRINT#15,"p"+CHR$(2)CHR$(L)CHR$(H)CHR$(1):PRINT#1,"999"
1070 CLOSE1:CLOSE15:OPEN15,8,15:GOSUB580:CLOSE15:GOTO160
1080 M$(0)=NP$:REM VIEW SCORES
1090 B=99:GOSUB320:OPEN1,8,2,"0:"+NP$:OPEN15,8,15:T(1)=0:T(2)=0:REM +",l,"+CHR$(5)
1100 PRINT"{clear}{down}{right*6}"NP$"'s scoring":T(3)=0:T(4)=0:T(5)=0:T(6)=0
1110 PRINT" 0-34 35-49 50-64 65-79 80-94 wow!!{down}":Q=0:T=0
1120 L=2:H=0:PRINT#15,"p"+CHR$(2)+CHR$(1)+CHR$(0)+CHR$(1):INPUT#1,HS$:HS=VAL(HS$)
1130 L=L+1:IFL=256THENL=0:H=H+1
1140 IFHS=0THENCLOSE1:CLOSE15:GOTO1260
1150 PRINT#15,"p"+CHR$(2)+CHR$(L)+CHR$(H)+CHR$(1)
1160 INPUT#1,X$:IFX$="999"THEN1240
1170 X=VAL(X$):Q=Q+X:T=T+1
1180 IFX>94THENT(6)=T(6)+1:GOTO1130
1190 IFX>79THENT(5)=T(5)+1:GOTO1130
1200 IFX>64THENT(4)=T(4)+1:GOTO1130
1210 IFX>49THENT(3)=T(3)+1:GOTO1130
1220 IFX>34THENT(2)=T(2)+1:GOTO1130
1230 T(1)=T(1)+1:GOTO1130
1240 CLOSE1:CLOSE15:T=INT((Q/T)*100+.5)/100
1250 PRINTTAB(0)T(1)TAB(6)T(2)TAB(12)T(3)TAB(18)T(4)TAB(24)T(5)TAB(30)T(6)
1260 PRINT"{down}{right*2}highest score"HS
1270 PRINT"{down}{right*2}average score"T
1280 PRINT"{down*2}{right*3}press return to cont.":WAIT197,13:POKE198,0
1290 NP$=M$(0):GOTO160
1300 M$(0)=NP$:B=99:GOSUB320:M$(Y)="----------":GOSUB1780
1310 OPEN15,8,15:PRINT#15,"s0:"+NP$:GOSUB580:CLOSE15:NP$=M$(0):GOTO160
1320 PRINT"{home}":FORX=1TO3:PRINT"{right*9}";:FORY=1TO4
1330 PRINTB$"{up*5}{right*2}";:NEXT:PRINT"{down*4}":NEXT
1340 PRINT"{right*9}";:FORY=1TO3:PRINTB$"{up*5}{right*2}";:NEXT:PRINTB$"{home}":RETURN
1350 FORX=1TO52:Z(X)=PEEK(40706+X):NEXT:RETURN:REM LOAD PRE-SHUFFLED DECK
1360 LB=B:B=PEEK(56321):GETZA$
1370 IFZA$=""ANDB=255THEN1360:REM PROCESS MOVEMENT W Z A S
1380 IFLB=BANDZA$=""THENLB=0:GOTO1360
1390 IFB=239THEN1490
1400 IFB=254THENSY=SY-1:IFSY<0THENSY=3:REM UP
1410 IFB=253THENSY=SY+1:IFSY=4THENSY=0:REM DOWN
1420 IFB=251THENSX=SX-1:IFSX<0THENSX=3:REM LEFT
1430 IFB=247THENSX=SX+1:IFSX=4THENSX=0:REM RIGHT
1440 IFZA$="w"THENSY=SY-1:IFSY<0THENSY=3:REM UP
1450 IFZA$="z"THENSY=SY+1:IFSY=4THENSY=0:REM DOWN
1460 IFZA$="a"THENSX=SX-1:IFSX<0THENSX=3:REM LEFT
1470 IFZA$="s"THENSX=SX+1:IFSX=4THENSX=0:REM RIGHT
1480 M=O:O=1192+SY*240+SX*8:POKEM,32:POKEO,62:GOTO1360
1490 IFR(SY,SX)>0THEN1360
1500 R(SY,SX)=Q:F(SY,SX)=A
1510 IFR(SY,SX)>10THENS(SY,SX)=10:GOTO1530
1520 S(SY,SX)=R(SY,SX)
1530 POKE198,0:GOSUB1620:GOSUB1630
1540 GETZA$:IFZA$=""THEN1540
1550 IFZA$="b"THENGOSUB1620:PRINT"{space*2}{down*3}{space*2}":R(SY,SX)=0:RETURN
1560 R(5,SX)=R(5,SX)+1:R(SY,5)=R(SY,5)+1:RETURN
1570 IFJ<16THEN1590
1580 A=F(0,4):Q=H:PRINT"{home}{right*2}{down*11}";:POKE198,0:GOSUB1630
1590 IFR(SY,5)=4THENT=SY:R(SY,5)=0:PRINT"{home}{right*2}{down*5}{space*2}{down*3}{space*2}":GOSUB1870
1600 IFR(5,SX)=4THENT=SX:R(5,SX)=0:PRINT"{home}{right*2}{down*5}{space*2}{down*3}{space*2}":GOSUB2170
1610 RETURN
1620 PRINT"{home}{down*2}{right*10}";SPC(SX*8);:POKE214,SY*6+2:SYS58732:RETURN
1630 PRINTMID$(K$,A,1)MID$(C$,Q,1)MID$(S$,A,1)Y$MID$(C$,Q,1)MID$(S$,A,1):RETURN
1640 IFB=99THENRETURN
1650 PRINT"{home}"D$"{down*6}{space*2}please enter your name"
1660 ZZ$="":POKE204,0
1670 GETZA$:IFZA$=""THEN1670
1680 IFASC(ZA$)=(13)THENPRINT" ":POKE204,1:GOTO1760
1690 IFASC(ZA$)=(20)THEN1730
1700 IFASC(ZA$)<65ORASC(ZA$)>90THEN1670
1710 ZZ$=ZZ$+ZA$
1720 PRINTZA$;:GOTO1670
1730 IFLEN(ZZ$)>1THENZZ$=LEFT$(ZZ$,LEN(ZZ$)-1):GOTO1720
1740 IFLEN(ZZ$)=1THENZZ$="":GOTO1720
1750 GOTO1670
1760 IFLEN(ZZ$)=0THEN1660
1770 M$(Y)=ZZ$:GOSUB1810
1780 OPEN1,8,2,"@0:squares,s,w"
1790 FORX=1TO10:PRINT#1,M$(X):NEXT:CLOSE1
1800 RETURN
1810 OPEN15,8,15:OPEN1,8,2,"0:"+M$(Y)+",l,"+CHR$(5)
1820 PRINT#15,"p"+CHR$(2)+CHR$(0)+CHR$(2)+CHR$(1):PRINT#1,"999":REM 999RECORDS
1830 PRINT#15,"p"+CHR$(2)+CHR$(1)+CHR$(0)+CHR$(1):PRINT#1,"0"
1840 PRINT#15,"p"+CHR$(2)+CHR$(2)+CHR$(0)+CHR$(1):PRINT#1,"3"
1850 PRINT#15,"p"+CHR$(2)+CHR$(3)+CHR$(0)+CHR$(1):PRINT#1,"999"
1860 CLOSE1:CLOSE15:RETURN
1870 POKE56334,PEEK(56334)AND254
1880 RESTORE:C=0:FORX=1TO4:IFF(T,0)=F(T,X)THENC=C+1:IFC=3ANDX=3THENP=P+4
1890 NEXT:IFC=4THENP=P+1
1900 FORY=0TO3:IFR(T,Y)=11ANDF(T,Y)=VTHENP=P+1
1910 NEXT
1920 FORY=0TO2:FORX=Y+1TO4:IFR(T,Y)=R(T,X)THENP=P+2
1930 NEXT:NEXT:IFR(T,3)=HTHENP=P+2
1940 IFS(T,0)+S(T,1)+S(T,2)+S(T,3)+H=15THENP=P+2:GOTO2010
1950 FORY=0TO4:READA,B,C,D:IFS(T,A)+S(T,B)+S(T,C)+S(T,D)=15THENP=P+2
1960 NEXT
1970 FORY=0TO9:READA,B,C:IFS(T,A)+S(T,B)+S(T,C)=15THENP=P+2
1980 NEXT
1990 FORY=0TO9:READA,B:IFS(T,A)+S(T,B)=15THENP=P+2
2000 NEXT
2010 RESTORE
2020 FORY=0TO4:S(T,Y)=R(T,Y):NEXT
2030 FORX=0TO3:FORY=X+1TO4
2040 IFS(T,X)>S(T,Y)THENK=S(T,X):S(T,X)=S(T,Y):S(T,Y)=K
2050 NEXT:NEXT
2060 K=S(T,0)+1
2070 IFK=S(T,1)ANDK+1=S(T,2)ANDK+2=S(T,3)ANDK+3=S(T,4)THENP=P+5:GOTO2140
2080 RESTORE-20:FORY=0TO4:READA,B,C,D:K=S(T,A)+1
2090 IFK=S(T,B)ANDK+1=S(T,C)ANDK+2=S(T,D)THENP=P+4:L=99
2100 NEXT:IFL=99THEN2140
2110 FORY=0TO9:READA,B,C:K=S(T,A)+1
2120 IFK=S(T,B)ANDK+1=S(T,C)THENP=P+3
2130 NEXT
2140 FORX=0TO4:IFR(T,X)>10THENS(T,X)=10:GOTO2160
2150 S(T,X)=R(T,X)
2160 NEXT:POKE56334,PEEK(56334)OR1:RETURN
2170 POKE56334,PEEK(56334)AND254
2180 RESTORE-20:C=0:FORX=1TO4:IFF(0,T)=F(X,T)THENC=C+1:IFC=3ANDX=3THENP=P+4
2190 NEXT:IFC=4THENP=P+1
2200 FORY=0TO3:IFR(Y,T)=11ANDF(Y,T)=VTHENP=P+1
2210 NEXT
2220 FORY=0TO2:FORX=Y+1TO4:IFR(Y,T)=R(X,T)THENP=P+2
2230 NEXT:NEXT:IFR(3,T)=HTHENP=P+2
2240 IFS(0,T)+S(1,T)+S(2,T)+S(3,T)+H=15THENP=P+2:GOTO2310
2250 FORY=0TO4:READA,B,C,D:IFS(A,T)+S(B,T)+S(C,T)+S(D,T)=15THENP=P+2
2260 NEXT
2270 FORY=0TO9:READA,B,C:IFS(A,T)+S(B,T)+S(C,T)=15THENP=P+2
2280 NEXT
2290 FORY=0TO9:READA,B:IFS(A,T)+S(B,T)=15THENP=P+2
2300 NEXT
2310 RESTORE
2320 FORY=0TO4:S(Y,T)=R(Y,T):NEXT
2330 FORX=0TO3:FORY=X+1TO4
2340 IFS(X,T)>S(Y,T)THENK=S(X,T):S(X,T)=S(Y,T):S(Y,T)=K
2350 NEXT:NEXT
2360 K=S(0,T)+1
2370 IFK=S(1,T)ANDK+1=S(2,T)ANDK+2=S(3,T)ANDK+3=S(4,T)THENP=P+5:GOTO2440
2380 RESTORE-20:FORY=0TO4:READA,B,C,D:K=S(A,T)+1
2390 IFK=S(B,T)ANDK+1=S(C,T)ANDK+2=S(D,T)THENP=P+4:L=99
2400 NEXT:IFL=99THEN2440
2410 FORY=0TO9:READA,B,C:K=S(A,T)+1
2420 IFK=S(B,T)ANDK+1=S(C,T)THENP=P+3
2430 NEXT
2440 FORX=0TO4:IFR(X,T)>10THENS(X,T)=10:GOTO2460
2450 S(X,T)=R(X,T)
2460 NEXT:POKE56334,PEEK(56334)OR1:RETURN
